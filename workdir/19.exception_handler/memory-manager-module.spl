alias output R0;
alias functionNum R1;
alias arg1 R2;
alias arg2 R3;
alias arg3 R4;

// get free page
if(functionNum == 1) then

	alias pid R5;
 	pid = [SYSTEM_STATUS_TABLE + 1];

	// increment wait mem count (not in busy loop as its not decremented automatically on memory release)
	[SYSTEM_STATUS_TABLE + 3] = [SYSTEM_STATUS_TABLE + 3] + 1;

	// when mem free count is 0
	while([SYSTEM_STATUS_TABLE + 2] == 0) do

		// update state of invoked process
		[PROCESS_TABLE + (pid * PT_ENTRY_SIZE) + 4] = "WAIT_MEM";

		// schedule other process
		multipush(R1, R2, R3, R4, R5);
		call MOD_5;
		multipop(R1, R2, R3, R4, R5);
	endwhile;

	// executed when process is scheduled again after its state is updated by interrupt denoting free available memory
	// Decrement the WAIT_MEM_COUNT field and MEM_FREE_COUNT
	[SYSTEM_STATUS_TABLE + 3] = [SYSTEM_STATUS_TABLE + 3] - 1;
	[SYSTEM_STATUS_TABLE + 2] = [SYSTEM_STATUS_TABLE + 2] - 1;

	// get free page starting from 76 ( user allowed paged are from 76 - 127 )
	alias counter R6;
	counter = 76;
	while(counter < MAX_MEM_PAGE) do
		 if([MEMORY_FREE_LIST + counter] == 0) then
			[MEMORY_FREE_LIST + counter] = 1;
			output = counter;
			break;
		 endif;
		 counter = counter + 1;
	endwhile;

	return;
endif;

// release page
if(functionNum == 2) then

	// decrement value of page in mem free list
	if([MEMORY_FREE_LIST + arg1] != 0) then
		[MEMORY_FREE_LIST + arg1] = [MEMORY_FREE_LIST + arg1] - 1;
	endif;

	// update mem free count if page is free
	if([MEMORY_FREE_LIST + arg1] == 0) then
		[SYSTEM_STATUS_TABLE + 2] = [SYSTEM_STATUS_TABLE + 2] + 1;

		//Update the STATE to READY for all processes (with valid PID) which have STATE as WAIT_MEM
		alias pid R5;
		alias ptentry R6;
		pid = 1;
		while(pid < MAX_PROC_NUM) do
			ptentry = PROCESS_TABLE + (pid * PT_ENTRY_SIZE);
			if([ptentry + 4] == "WAIT_MEM") then
				[ptentry + 4] = "READY";
			endif;
		  pid = pid + 1;
		endwhile;
	endif;
	return;
endif;

// release block
//arg1:blockNum;arg2:pid
if(functionNum == 4) then
	// Decrement the count of the disk block number in the memory copy of the Disk Free List
	[DISK_FREE_LIST + arg1] = [DISK_FREE_LIST + arg1] - 1;

	return;
endif;

// get code page
//arg1:blockNum
if(functionNum == 5) then
	alias pid R5;
	alias diskMapEntry R6;
	alias diskMapCounter R7;
	alias ptbr R8;
	pid = 0;
	// if code block is already loaded to memory by some process, share the memory space as its read-only
	while (pid < MAX_PROC_NUM ) do
		diskMapEntry = DISK_MAP_TABLE + pid * 10;
		ptbr = PAGE_TABLE_BASE + pid * 20;
		diskMapCounter = 0;
		while( diskMapCounter < 4 ) do
			if( ([diskMapEntry + 4 + diskMapCounter] == arg1) && ([ptbr + 8 + (2 * diskMapCounter)] != -1)) then
				R0 = [ptbr + 8 + (2 * diskMapCounter)];
				[MEMORY_FREE_LIST + R0] = [MEMORY_FREE_LIST + R0] + 1;
				return;
			endif;
			diskMapCounter = diskMapCounter + 1;
		endwhile;
		pid = pid + 1;
	endwhile;

	//if code page not in memory
	//call get free page
	multipush(R1, R2, R3, R4, R5, R6, R7, R8);
	functionNum = 1;
	call MOD_2;
	alias freePage R9;
	freePage = R0;
	multipop(R1, R2, R3, R4, R5, R6, R7, R8);

	//load disk-block to free page
	multipush(R1, R2, R3, R4, R5, R6, R7, R8, R9);
	functionNum = 2;
	arg3 = arg1;
	arg2 = freePage;
	arg1 = [SYSTEM_STATUS_TABLE + 1];
	call MOD_4;
	multipop(R1, R2, R3, R4, R5, R6, R7, R8, R9);

	R0 = freePage;
	return;
endif;

